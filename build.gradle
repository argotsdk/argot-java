apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

buildscript {
  repositories {
    jcenter()
    maven { 
      url "http://repo.maven.apache.org/maven2"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.javamodularity:moduleplugin:1.5.0"
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    
  }
}

// Development environments
apply plugin: 'idea'
apply plugin: 'eclipse'
eclipse {
    classpath {
        defaultOutputDir = new File("$buildDir/eclipse");
        file {
            whenMerged {
                entries.findAll { isModule(it) }.each {
                    it.entryAttributes['module'] = 'true'
                }

                entries.findAll { isSource(it) && isTestScope(it) }.each {
                    it.entryAttributes['test'] = 'true'
                }

                entries.findAll { isLibrary(it) && isTestScope(it) }.each {
                    it.entryAttributes['test'] = 'true'
                }
            }
        }
    }

}

group = 'org.argot-sdk'
version = '1.4.0-rc2'

sourceCompatibility = 11
targetCompatibility = 11



test {
	useJUnitPlatform()
}

modifyPom {
    project {
        name 'Argot'
        description 'A language for the Internet of Things.'
        url 'http://www.argot-sdk.org/'
        packaging 'jar'
        licenses {
            license {
                url 'https://github.com/argotsdk/argot-java/blob/master/LICENSE.TXT'
            }
        }
        scm {
            url 'https://github.com/argotsdk/argot-java'
            connection 'scm:git:https://github.com/argotsdk/argot-java.git'
            developerConnection 'scm:git@github.com:argotsdk/argot-java.git'
        }
        developers {
            developer {
                id 'oobles'
                name 'David Ryan'
            }
            developer {
                id 'danmidwood'
                name 'Dan Midwood'
            }
        }

    }
}

boolean isLibrary(entry) {
  return entry.properties.kind.equals('lib') || entry.properties.kind.equals('src')
}
boolean isTestScope(entry) {
  return entry.entryAttributes.get('gradle_used_by_scope').equals('test'); }
boolean isModule(entry) {
  return isLibrary(entry) && !isTestScope(entry); }
boolean isSource(entry) { return entry.properties.kind.equals('src'); }


